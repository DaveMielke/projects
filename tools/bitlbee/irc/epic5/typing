package typing

assign typing.timeout 10

on #-channel_signoff 20 '*' (channel, name, message) {
   typing.end $name
}

on #-msg 20 '*' (sender, message) {
   typing.end $sender
}

on ^ctcp '% $N TYPING *' (sender, receiver, action, arg, void) {
   if (arg) {
      typing.begin $sender
   } else {
      typing.end $sender
   }
}

alias typing.begin (name, void) {
   typing.play $name
}

alias typing.end (name, void) {
   assign -typing[time][$name]
}

alias typing.playing (name, void) {
   return $@typing[time][$name]
}

alias typing.play (name, void) {
   if (sessions.create($name 1) == windowctl(refnum 0)) {
      local now $time()

      if (typing.playing($name)) {
         if ((now - [$typing[time][$name]]) < typing.timeout) {
            return
         }
      }

      assign typing[time][$name] $now
      sounds.play typing
   }
}
